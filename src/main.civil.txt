# addStyle helper
.. style @ -- document createElement ! >> styleTag
.. $styleTag @ -- document head appendChild !
.. $styles >> styleTag textContent
<< styles >> addStyle

# load global styles
./src/globalStyle.civil.txt @ load !

# load all components
$Object ! >> components
.. './src/components/$name .civil.txt' @ -- load !
.. >> -- components $name
<< name : \
 button database debounce delay dialog \
 documentCreateDialog documentDelete documentDetails \
 documentDownload documentManageDialog documentManageDelete \
 documentView documentsList documentsStore \
 input menu newsFeed \
 spacer toggle \
!

# Components , $components @ console log !

# news feed
$components @ components newsFeed ! >> newsFeed
$newsFeed flash @ document body appendChild !

# create explorer
div @ document createElement ! >> explorer
explorer @ explorer classList add !
$explorer @ document body appendChild !

# connect to documents store
Civil @ components database ! >> civilDatabase
# civilDatabase , $civilDatabase @ console log !
$civilDatabase @ components documentsStore ! >> Documents
# Documents , $Documents @ console log !

# function to save a document
.. $-- Object ! >> documentObject
.. $id >> documentObject id
.. '/' @ id lastIndexOf ! >> nameDirSplit
.. $nameDirSplit +1 >> nameDirSplit
.. $nameDirSplit @ id substring ! >> documentObject name
.. 0 , $nameDirSplit @ id substring ! >> documentObject dir
.. $value >> documentObject value
.. $documentObject id @ -- Documents get ! >> existingDocument
.. .. .. $-- -- existingDocument created >> -- -- documentObject created
.. .. $-- existingDocument created ?
.. .. .. $-- -- -- Date now ! >> -- -- documentObject created
.. .. $-- existingDocument created ~ ?
.. .. $-- existingDocument active >> -- documentObject active
.. .. $-- existingDocument expanded >> -- documentObject expanded
.. .. $-- existingDocument view >> -- documentObject view
.. $existingDocument ?
.. .. $-- documentObject , $-- additionalProperties @ -- -- Object assign !
.. $additionalProperties ?
.. $-- Date now ! >> documentObject updated
.. $documentObject @ -- Documents put ! >> saveResult
.. # 'Saved document' , $documentObject id , $saveResult @ -- console log !
.. .. Home >> -- decodedName
.. $id = '/' ?
.. .. $-- documentObject name @ -- -- decodeURIComponent ! >> -- decodedName
.. $id ~= '/' ?
.. 'Saved \'$decodedName \'' @ -- newsFeed news !~
.. ..
.. .. .. .. .. $-- -- -- document view update !
.. .. .. .. $-- -- document view ?
.. .. .. $-- document doc id ~= $-- -- id ?
.. .. .. .. .. $-- -- -- -- documentObject @ -- -- -- document view update !
.. .. .. .. $-- -- document view ?
.. .. .. $-- document doc id = $-- -- id ?
.. .. $document doc id @ -- id startsWith ! ?
.. << document :: -- allDocuments !
<< id value additionalProperties >> saveDocument

# function to rename a document
.. '$dir $oldName' >> oldId
.. $oldId @ -- Documents get ! >> documentObject
.. '$dir $newName' >> documentObject id
.. $newName >> documentObject name
.. $oldId , $documentObject @ -- Documents replace ! >> saveResult
.. # 'Renamed document' , $saveResult @ -- console log !
<< dir oldName newName >> renameDocument

# test create document
# '/' , 'test document' @ saveDocument !
# '/foo' , 'test document 2' @ saveDocument !
# '/foo/bar' , 'test document 3' @ saveDocument !
# '/foo/bar/bat' , 'test document 3' @ saveDocument !
# '/foo/bar/bat/baz' , 'test document 3' @ saveDocument !

# function to open a document
$Array ! >> allDocuments
.. '/' @ dir split ! >> dirSegments
.. $-- Array ! >> linkSegments
.. .. $segment @ -- linkSegments push !
.. .. '/' @ -- linkSegments join ! >> segmentFullPath
.. .. $-- -- allDocuments $index >> existingDocument
.. .. $segmentFullPath >> documentId
.. .. .. '/' >> -- documentId
.. .. $segmentFullPath = '' ?
.. .. .. .. $-- -- existingDocument >> -- -- document
.. .. .. $-- existingDocument doc id = $-- documentId ?
.. .. .. .. $-- -- segmentFullPath , $-- -- -- -- explorer , $-- -- -- -- Documents , $-- -- -- -- saveDocument , $-- -- -- -- renameDocument , \
             $-- -- -- -- components @ -- -- -- -- components documentDetails ! >> -- -- document
.. .. .. $-- existingDocument doc id ~= $-- documentId ?
.. .. $existingDocument ?
.. .. .. $-- segmentFullPath , $-- -- -- explorer , $-- -- -- Documents , $-- -- -- saveDocument , $-- -- -- renameDocument , \
          $-- -- -- components @ -- -- -- components documentDetails ! >> -- document
.. .. $existingDocument ~ ?
.. .. $document
.. << segment index :: dirSegments ! >> newAllDocuments
.. .. $-- newAllDocuments $index >> newDocument
.. .. .. .. $-- -- existingDocument container remove !
.. .. .. $-- newDocument doc id ~= $-- existingDocument doc id ? 
.. .. $newDocument ?
.. .. .. $-- existingDocument container remove !
.. .. $newDocument ~ ?
.. << existingDocument index :: -- allDocuments !
.. $newAllDocuments >> -- allDocuments
.. $-- allDocuments $--\ allDocuments\ length\ -1 show !
.. .. $document tree reload !
.. << document :: -- allDocuments !
<< dir >> openDocument

# open document on hash navigation event
.. 1 @ -- location hash substring ! @ -- openDocument !
<< >> route
hashchange , $route @ addEventListener !
$route !

# cross-browser styles
$navigator appCodeName toLowerCase ! >> appCodeName
'navigator-$appCodeName' @ document body classList add !

# handle cross-frame messages
.. # 'Incoming message' , $message @ -- console log !
.. $-- Object ! >> response
.. .. $-- message data data href >> newHash
.. .. '/#' @ newHash startsWith ! >> isAbsolute
.. .. .. 2 @ -- newHash substring ! >> -- -- -- location hash
.. .. $isAbsolute ?
.. .. .. '$--\ --\ --\ location\ hash $--\ newHash' >> -- -- -- location hash
.. .. $isAbsolute ~ ?
.. $message data type = navigate ?
.. $message data requestId >> response requestId
.. .. $-- message data basePath >> basePath
.. .. '$basePath $--\ message\ data\ data\ path' @ -- -- Documents get ! >> -- reply
.. $message data type = read ?
.. .. $-- message data basePath >> basePath
.. .. '$basePath $--\ message\ data\ data\ path' , $-- message data data viewPath @ -- -- Documents getFromView ! >> -- reply
.. $message data type = readView ?
.. .. $-- message data basePath >> basePath
.. .. '$basePath $--\ message\ data\ data\ path' @ -- -- Documents getByDir ! >> -- reply
.. $message data type = readDir ?
.. .. $-- message data basePath >> basePath
.. .. '$basePath $--\ message\ data\ data\ path' @ -- -- Documents getNamesByDir ! >> -- reply
.. $message data type = readDirNames ?
.. .. $-- message data basePath >> basePath
.. .. '$basePath $--\ message\ data\ data\ path' , $-- message data data value , \
       $-- message data data additionalProperties @ -- -- saveDocument ! >> -- reply
.. $message data type = write ?
.. $reply >> response data
.. $response @ message source postMessage !
<< message >> handleMessage
message , $handleMessage @ addEventListener !

